PREFIX daq:<http://purl.org/eis/vocab/daq#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>
PREFIX sdmx-dimension:<http://purl.org/linked-data/sdmx/2009/dimension#>

SELECT DISTINCT ?metric ?value {
	?obs a daq:Observation ;
		daq:metric ?metric_instance ;
		daq:value ?value ;
		sdmx-dimension:timePeriod ?dateTime 
	
	# This filter eliminates any solution (triples) that
	# have a date time older than the newest observation
	FILTER NOT EXISTS {
       	?obs2 daq:metric ?metric_instance ;
          	  sdmx-dimension:timePeriod ?newerDateTime  .
      	FILTER (?newerDateTime > ?dateTime) 
	} 
  
  	?metric_instance a ?metric .
} ORDER BY ASC(?metric)